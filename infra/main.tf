module "acr" {
  source              = "./modules/acr"
  resource_group_name = var.resource_group_name
  location            = var.location
  name                = var.acr_name
  sku                 = var.acr_sku
}

module "postgres" {
  source              = "./modules/postgres-flexible"
  resource_group_name = var.resource_group_name
  location            = coalesce(var.pg_location, var.location)
  name                = var.pg_name
  pg_version          = var.pg_version
  sku                 = var.pg_sku
  storage_mb          = var.pg_storage_mb
  # no need to supply login and password, will be auto generated by the module
}

module "kubernetes" {
  source              = "./modules/aks"
  resource_group_name = var.resource_group_name
  location            = var.location
  name                = var.aks_name
  node_count          = var.aks_node_count
  node_vm_size        = var.aks_node_vm_size

  # Allow the AKS cluster to pull images from the ACR
  attached_container_registries = [
    module.acr.id
  ]
}

# Store information on resources in Key Vault, where other microservices can access them
module "keyvault" {
  source              = "./modules/keyvault"
  name                = var.keyvault_name
  resource_group_name = var.resource_group_name
  location            = var.location
  secrets = {
    "acr-login-server"  = module.acr.login_server
    "pg-fqdn"           = module.postgres.fqdn
    "pg-admin-username" = module.postgres.administrator_login
    "pg-admin-password" = module.postgres.administrator_password
    "aks-kube-config"   = module.kubernetes.kube_config
  }
}
